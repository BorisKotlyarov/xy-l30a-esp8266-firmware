#include "esp8266withXYL30A.h"
#include <ESP8266WiFi.h>
#include <ESP8266WebServer.h>
#include <SoftwareSerial.h>
#include <PubSubClient.h>
#include <ArduinoJson.h>
#include <EEPROM.h>
#include <user_interface.h>
#include "XYParser.h"
#include "WifiConnectionManager.h"
#include "config.h"
#include "HttpConfigServer.h"

// UART –¥–ª—è XY-L30A
SoftwareSerial loraSerial(3, 1); // RX = GPIO3, TX = GPIO1
HttpConfigServer configServer(80, saveConfigToEEPROM, resetWiFiCredentials);

WiFiClient espClient;
PubSubClient mqttClient(espClient);

char WIFI_SSID[64] = {0};
char WIFI_PASSWORD[64] = {0};
char MQTT_SERVER[64] = {0};
char MQTT_USER[64] = {0};
char MQTT_PASS[64] = {0};
char MQTT_CLIENT_ID[64] = {0};
char authUser[32] = {0};
char authPass[32] = {0};

int MQTT_PORT = 1883;

// –ü—Ä–æ—Ç–æ—Ç–∏–ø—ã
// prototypes.h

bool isSerialDebug = false; // –≤–∫–ª—é—á—ë–Ω —Ä–µ–∂–∏–º –æ—Ç–ª–∞–¥–∫–∏, UART (if==true:  loraSerial –ù–ï –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∏—Ä—É–µ—Ç—Å—è)
unsigned long lastMqttAttempt = 0;
const unsigned long mqttRetryInterval = 5000; // –≤ –º—Å
unsigned int WifiattemptReconnect = 0;
unsigned int MAX_ATTEMPT_TO_RECONNECT = 10; // arter that device will reboot

const int EEPROM_SIZE = 512;

const int OFFSET_WIFI_SSID = 0;
const int OFFSET_WIFI_PASS = 64;
const int OFFSET_MQTT_SERVER = 128;
const int OFFSET_MQTT_PORT = 192;
const int OFFSET_MQTT_USER = 200;
const int OFFSET_MQTT_PASS = 264;
const int OFFSET_MQTT_CLIENT_ID = 328;
const int OFFSET_AUTH_USER = 448;
const int OFFSET_AUTH_PASS = 480;

void setup()
{

  Serial.begin(115200);
  delay(1000);

  pinMode(LED_BUILTIN, OUTPUT);
  Serial.println("=== Let's start ===");

  EEPROM.begin(EEPROM_SIZE);

  // –ß—Ç–µ–Ω–∏–µ –∫–æ–Ω—Ñ–∏–≥—É—Ä–∞—Ü–∏–∏
  readStringFromEEPROM(OFFSET_WIFI_SSID, WIFI_SSID, sizeof(WIFI_SSID));
  readStringFromEEPROM(OFFSET_WIFI_PASS, WIFI_PASSWORD, sizeof(WIFI_PASSWORD));
  loadAuthFromEEPROM();

  if (strlen(WIFI_SSID) == 0)
  {
    Serial.println("üì° SSID not found. Starting Wireless Connection Manager");
    initLogin();
  }
  else
  {
    connectToAP(WIFI_SSID, WIFI_PASSWORD, true);
  }

  configServer.setIsSerialDebug(isSerialDebug);
  if (!isSerialDebug)
  {
    loraSerial.begin(9600); // UART –¥–ª—è XY-L30A –∞–∫—Ç–∏–≤–µ–Ω, –µ—Å–ª–∏ –ù–ï Serial Debug
    configServer.setLoraSerial(&loraSerial);
  }

  if (WiFi.status() == WL_CONNECTED)
  {
    Serial.println("\n‚úÖ Wi-Fi –ø–æ–¥–∫–ª—é—á–µ–Ω");
    Serial.print("IP ESP: ");
    Serial.println(WiFi.localIP());
  }
  else
  {
    Serial.println("\n‚ùå Wi-Fi –Ω–µ —É–¥–∞–ª–æ—Å—å –ø–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è");
    // –ú–æ–∂–µ—à—å —Ä–µ—à–∞—Ç—å: –ø–µ—Ä–µ–∑–∞–ø—É—Å—Ç–∏—Ç—å, –∂–¥–∞—Ç—å –≤ loop –∏ –ø—ã—Ç–∞—Ç—å—Å—è –ø–æ–≤—Ç–æ—Ä–Ω–æ
    ESP.restart();
    return;
  }
  loadConfigFromEEPROM();

  configServer.begin();

  Serial.print("MQTT IP: ");
  Serial.println(MQTT_SERVER);

  mqttClient.setServer(MQTT_SERVER, (uint16_t)MQTT_PORT);
  mqttClient.setCallback(callback);
  Serial.print("MQTT PORT: ");
  Serial.println(MQTT_PORT);

  connectMQTT(true);
}

void loop()
{

  if (!wifiConnectionCheckAndRenew())
  {
    if (WifiattemptReconnect >= MAX_ATTEMPT_TO_RECONNECT)
    {
      delay(2000);
      ESP.restart();
    }

    return;
  }
  else
  {
    WifiattemptReconnect = 0; // reset attempt
  }

  configServer.loop();

  if (!isSerialDebug)
  {
    loraReader();
  }

  if (!mqttClient.connected())
  {
    connectMQTT(false);
    configServer.setMqttConnected(false);
  }
  else
  {
    configServer.setMqttConnected(true);
    mqttClient.loop();
    publishStatus();
  }
}

void publishStatus()
{
  static unsigned long lastMqttStatusTime = 0;
  const unsigned long statusInterval = 5000;

  unsigned long now = millis();
  if (now - lastMqttStatusTime < statusInterval)
    return;
  lastMqttStatusTime = now;

  // –§–æ—Ä–º–∏—Ä—É–µ–º uptime
  unsigned long uptimeSec = millis() / 1000;
  int hrs = uptimeSec / 3600;
  int min = (uptimeSec % 3600) / 60;
  int sec = uptimeSec % 60;

  char uptimeStr[12];
  snprintf(uptimeStr, sizeof(uptimeStr), "%02d:%02d:%02d", hrs, min, sec);

  StaticJsonDocument<192> doc;
  doc["status"] = "online";
  doc["ip"] = WiFi.localIP().toString();
  doc["rssi"] = String(WiFi.RSSI());
  doc["uptime"] = String(uptimeStr);
  doc["device_id"] = MQTT_CLIENT_ID;

  String jsonOut;
  serializeJson(doc, jsonOut);

  mqttClient.publish("device/status", jsonOut.c_str(), true);
}

void resetWiFiCredentials()
{
  Serial.println("‚ö†Ô∏è –°–±—Ä–æ—Å Wi-Fi –∫–æ–Ω—Ñ–∏–≥—É—Ä–∞—Ü–∏–∏...");

  // –û—á–∏—Å—Ç–∏–º EEPROM –æ–±–ª–∞—Å—Ç–∏, –≥–¥–µ —Ö—Ä–∞–Ω—è—Ç—Å—è SSID –∏ –ø–∞—Ä–æ–ª—å
  for (int i = OFFSET_WIFI_SSID; i < OFFSET_WIFI_PASS + 64; i++)
  {
    EEPROM.write(i, 0); // –û–±–Ω—É–ª—è–µ–º –±–∞–π—Ç—ã
  }
  EEPROM.commit();

  // –û—Ç–∫–ª—é—á–∞–µ–º—Å—è –æ—Ç —Å–µ—Ç–∏ –∏ —Å–±—Ä–∞—Å—ã–≤–∞–µ–º –∞–≤—Ç–æ-–ø–æ–¥–∫–ª—é—á–µ–Ω–∏–µ
  WiFi.disconnect(true); // true = —Å–±—Ä–æ—Å –∏ —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è
  delay(100);

  WiFi.mode(WIFI_OFF);
  delay(100);

  Serial.println("‚úÖ Wi-Fi –∫–æ–Ω—Ñ–∏–≥—É—Ä–∞—Ü–∏—è —Å–±—Ä–æ—à–µ–Ω–∞");

  // –ü–µ—Ä–µ–∑–∞–≥—Ä—É–∑–∏–º –¥–ª—è –ø—Ä–∏–º–µ–Ω–µ–Ω–∏—è
  ESP.restart();
}

void blink(int _delay = 500, int num = 1)
{
  for (int i = 0; i < num; i++)
  {
    digitalWrite(LED_BUILTIN, LOW); // –í–∫–ª—é—á–∏—Ç—å (LOW ‚Äî –∞–∫—Ç–∏–≤–Ω—ã–π —É—Ä–æ–≤–µ–Ω—å –Ω–∞ ESP)
    delay(_delay);
    digitalWrite(LED_BUILTIN, HIGH); // –í—ã–∫–ª—é—á–∏—Ç—å
    delay(_delay);
  }
}

bool wifiConnectionCheckAndRenew()
{
  static unsigned long lastWiFiRetry = 0;
  const unsigned long wifiRetryInterval = 5000;

  if (WiFi.status() != WL_CONNECTED)
  {
    unsigned long _now = millis();
    if (_now - lastWiFiRetry > wifiRetryInterval)
    {
      Serial.println("üîÑ Wi-Fi –ø–æ—Ç–µ—Ä—è–Ω. –ü–µ—Ä–µ–ø–æ–¥–∫–ª—é—á–µ–Ω–∏–µ...");
      WiFi.reconnect();
      lastWiFiRetry = _now;
      blink(50);
    }
    return false;
  }

  return true;
}

void saveStringToEEPROM(int addr, const String &value)
{
  Serial.print("üíæ Save to EEPROM @");
  Serial.print(addr);
  Serial.print(": [");
  Serial.print(value);
  Serial.println("]");

  for (int i = 0; i < value.length(); ++i)
  {
    EEPROM.write(addr + i, value[i]);
  }
  EEPROM.write(addr + value.length(), '\0'); // –∑–∞–≤–µ—Ä—à–∞—é—â–∏–π 0
  EEPROM.commit();
}

void readStringFromEEPROM(int addr, char *buffer, size_t maxLen)
{
  int i = 0;
  char ch;
  while ((ch = EEPROM.read(addr + i)) != '\0' && i < maxLen - 1)
  {
    buffer[i++] = ch;
  }
  buffer[i] = '\0';
}

// –ó–∞–ø–∏—Å—å char[] –≤ EEPROM
void saveStringToEEPROM(int addr, const char *value)
{
  Serial.print("üíæ Save to EEPROM @");
  Serial.print(addr);
  Serial.print(": [");
  Serial.print(value);
  Serial.println("]");

  int i = 0;
  while (value[i] != '\0' && i < 63)
  {
    EEPROM.write(addr + i, value[i]);
    i++;
  }
  EEPROM.write(addr + i, '\0');
  EEPROM.commit();
}

void initLogin()
{
  WCMRun();

  Serial.println(WCMStatus());

  if (WCMStatus() == WCM_SUBMIT)
  {
    connectToAP(wcmConfig.SSID, wcmConfig.pass, true);

    if (WiFi.status() != WL_CONNECTED)
    {
      initLogin();
    }
    else if (WiFi.status() == WL_CONNECTED)
    {
      saveWiFiConfig(wcmConfig.SSID, wcmConfig.pass);
      Serial.println("‚úÖ Wi-Fi saved");
      return;
    }
  }
}

void connectToAP(const char *ssid, const char *pass, bool isCheckAttempt = false)
{
  if (WiFi.status() == WL_CONNECTED)
  {
    return;
  }
  int tryCount = 0;
  int ATTEMPT = 100; // each ATTEMPT == 1000ms

  // Connect to Wi-Fi
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, pass);

  while (true)
  {
    wl_status_t status = WiFi.status();
    if (status == WL_CONNECTED)
    {

      break;
    }
    blink(50);
    Serial.print('.');
    if (isCheckAttempt)
    {
      tryCount++;
      if (status == WL_CONNECT_FAILED || tryCount >= ATTEMPT)
      {

        return;
      }
    }
    delay(1000);
  }

  configTime(3 * 3600, 0, "pool.ntp.org", "time.nist.gov");

  time_t now = time(nullptr);
  while (now < 8 * 3600 * 2)
  {
    delay(500);
    Serial.print(".");
    now = time(nullptr);
  }

  struct tm timeinfo;
  gmtime_r(&now, &timeinfo);
  Serial.println(asctime(&timeinfo));
}

void saveWiFiConfig(const String &ssid, const String &pass)
{
  saveStringToEEPROM(OFFSET_WIFI_SSID, ssid);
  saveStringToEEPROM(OFFSET_WIFI_PASS, pass);
}

void loadAuthFromEEPROM()
{
  EEPROM.begin(512);

  readStringFromEEPROM(OFFSET_AUTH_USER, authUser, sizeof(authUser));
  readStringFromEEPROM(OFFSET_AUTH_PASS, authPass, sizeof(authPass));

  if (strlen(authUser) == 0 || !isAscii(authUser[0]))
  {
    strncpy(authUser, DEFAULT_USER, sizeof(authUser));
  }
  if (strlen(authPass) == 0 || !isAscii(authPass[0]))
  {
    strncpy(authPass, DEFAULT_PASS, sizeof(authPass));
  }

  Serial.println("üîê –ê–≤—Ç–æ—Ä–∏–∑–∞—Ü–∏—è:");
  Serial.print("User: [");
  Serial.print(authUser);
  Serial.println("]");

  // –í—Ä–µ–º–µ–Ω–Ω–æ–µ —Ä–µ—à–µ–Ω–∏–µ –¥–ª—è —Å–æ–≤–º–µ—Å—Ç–∏–º–æ—Å—Ç–∏:
  configServer.setAuth(String(authUser), String(authPass));
}

void loadConfigFromEEPROM()
{
  EEPROM.begin(512);

  readStringFromEEPROM(OFFSET_WIFI_SSID, WIFI_SSID, sizeof(WIFI_SSID));
  readStringFromEEPROM(OFFSET_WIFI_PASS, WIFI_PASSWORD, sizeof(WIFI_PASSWORD));
  readStringFromEEPROM(OFFSET_MQTT_SERVER, MQTT_SERVER, sizeof(MQTT_SERVER));

  char portStr[6] = {0};
  readStringFromEEPROM(OFFSET_MQTT_PORT, portStr, sizeof(portStr));
  MQTT_PORT = atoi(portStr);

  readStringFromEEPROM(OFFSET_MQTT_USER, MQTT_USER, sizeof(MQTT_USER));
  readStringFromEEPROM(OFFSET_MQTT_PASS, MQTT_PASS, sizeof(MQTT_PASS));
  readStringFromEEPROM(OFFSET_MQTT_CLIENT_ID, MQTT_CLIENT_ID, sizeof(MQTT_CLIENT_ID));

  // –í—Ä–µ–º–µ–Ω–Ω–æ–µ —Ä–µ—à–µ–Ω–∏–µ –¥–ª—è —Å–æ–≤–º–µ—Å—Ç–∏–º–æ—Å—Ç–∏:
  configServer.setMQTT(
      String(MQTT_SERVER),
      String(MQTT_PORT),
      String(MQTT_USER),
      String(MQTT_PASS),
      String(MQTT_CLIENT_ID));
}

void saveConfigToEEPROM(const char *mqtt_ip, const char *mqtt_port,
                        const char *user, const char *mqtt_pass,
                        const char *client_id,
                        const char *auth_user, const char *auth_pass)
{

  saveStringToEEPROM(OFFSET_MQTT_SERVER, mqtt_ip);
  saveStringToEEPROM(OFFSET_MQTT_PORT, mqtt_port);
  saveStringToEEPROM(OFFSET_MQTT_USER, user);
  saveStringToEEPROM(OFFSET_MQTT_PASS, mqtt_pass);
  saveStringToEEPROM(OFFSET_MQTT_CLIENT_ID, client_id);

  if (auth_user && strlen(auth_user) > 0 && isAscii(auth_user[0]))
  {
    saveStringToEEPROM(OFFSET_AUTH_USER, auth_user);
    Serial.println("‚úÖ Will be save to EEPROM with auth_user");
  }

  if (auth_pass && strlen(auth_pass) > 0 && isAscii(auth_pass[0]))
  {
    saveStringToEEPROM(OFFSET_AUTH_PASS, auth_pass);
    Serial.println("‚úÖ Will be save to EEPROM with auth_pass");
  }

  EEPROM.commit();
  Serial.println("‚úÖ EEPROM saved");
}

void connectMQTT(bool force = false)
{
  loadConfigFromEEPROM();
  if (strlen(MQTT_SERVER) == 0)
    return;

  if (WiFi.status() != WL_CONNECTED)
  {
    // Serial.println("üö´ Wi-Fi –Ω–µ –ø–æ–¥–∫–ª—é—á—ë–Ω ‚Äî MQTT –Ω–µ –∑–∞–ø—É—Å–∫–∞–µ–º");
    return;
  }

  unsigned long now = millis();
  if (!force && now - lastMqttAttempt < mqttRetryInterval)
    return;

  lastMqttAttempt = now;

  Serial.println("MQTT —Å–æ–µ–¥–∏–Ω–µ–Ω–∏–µ...");
  blink(100, 3);

  mqttClient.setServer(MQTT_SERVER, (uint16_t)MQTT_PORT);

  StaticJsonDocument<192> doc;
  doc["status"] = "offline";
  doc["device_id"] = MQTT_CLIENT_ID;
  String jsonOut;
  serializeJson(doc, jsonOut);

  if (mqttClient.connect(MQTT_CLIENT_ID, MQTT_USER, MQTT_PASS, "device/status", 1, true, jsonOut.c_str()))
  {
    Serial.println("‚úÖ MQTT Connected");
    configServer.setMqttConnected(true);
    mqttClient.subscribe("device/command");
  }
  else
  {
    Serial.print("‚ùå MQTT ERROR: ");
    Serial.println(mqttClient.state());
    configServer.setMqttConnected(false);
  }
}

void callback(char *topic, byte *payload, unsigned int length)
{
  Serial.print("topic:");
  Serial.println(topic);

  StaticJsonDocument<200> doc;
  DeserializationError error = deserializeJson(doc, payload, length);

  if (error)
  {
    Serial.print("‚ö†Ô∏è –û—à–∏–±–∫–∞ JSON: ");
    Serial.println(error.c_str());
    return;
  }

  const char *action = doc["action"];
  const char *value = doc["value"];
  const char *device_id = doc["reciever"];

  Serial.print("‚ö†Ô∏è device_id from JSON: ");
  Serial.println(device_id);

  Serial.print("‚ö†Ô∏è MQTT_CLIENT_ID: ");
  Serial.println(MQTT_CLIENT_ID);

  if (device_id && strcmp(device_id, MQTT_CLIENT_ID) == 0)
  {
    Serial.print("üì• JSON MQTT –ö–æ–º–∞–Ω–¥–∞: ");
    Serial.print(action);
    Serial.print(" ‚Üí ");
    Serial.println(value);

    handleMQTTCommand(String(action), String(value));
  }
}

void handleMQTTCommand(String action, String value)
{
  if (action == "restart")
  {
    ESP.restart();
  }
  else if (action == "blink")
  {
    blink(200, value.toInt());
  }
  else if (action == "uart_send")
  {
    loraSerial.print(value);
  }
  else if (action == "reset_wifi")
  {
    resetWiFiCredentials();
  }
  else
  {
    Serial.println("‚ö†Ô∏è –ù–µ–∏–∑–≤–µ—Å—Ç–Ω–∞—è –∫–æ–º–∞–Ω–¥–∞: " + action);
  }
}

void loraReader()
{
  static char loraBuffer[64];
  static byte index = 0;

  while (loraSerial.available())
  {
    char c = loraSerial.read();
    if (c == '\n')
    {
      loraBuffer[index] = '\0';
      if (index > 0)
        handleXYResponse(loraBuffer);
      index = 0;
    }
    else if (index < sizeof(loraBuffer) - 1)
    {
      loraBuffer[index++] = c;
    }
    else
    {
      index = 0;
    }
    yield();
  }
}

void handleXYResponse(const char *rawLine)
{
  XYPacket packet;

  // üß† –ü–æ–ø—ã—Ç–∫–∞ —Ä–∞—Å–ø–∞—Ä—Å–∏—Ç—å –∫–∞–∫ data
  if (XYParser::parse(rawLine, packet))
  {
    StaticJsonDocument<192> doc;
    doc["type"] = "data";
    doc["voltage"] = packet.voltage;
    doc["percent"] = packet.percent;
    doc["time"] = String(packet.hours) + ":" + String(packet.minutes);
    doc["state"] = packet.state;
    doc["device_id"] = MQTT_CLIENT_ID;

    String jsonOut;
    serializeJson(doc, jsonOut);
    mqttClient.publish("esp/data", jsonOut.c_str());
    return;
  }

  // üß† –ê–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–∞ ‚Äî –∫–æ–Ω—Ñ–∏–≥—É—Ä–∞—Ü–∏–æ–Ω–Ω—ã–µ –ø–∞—Ä–∞–º–µ—Ç—Ä—ã
  StaticJsonDocument<256> doc;
  doc["type"] = "config";
  doc["device_id"] = MQTT_CLIENT_ID;
  JsonObject params = doc.createNestedObject("params");

  const char *knownKeys[] = {"dw", "up", "th", "st", "et", nullptr};
  char buffer[64];
  strncpy(buffer, rawLine, sizeof(buffer) - 1);
  buffer[sizeof(buffer) - 1] = '\0';

  char *token = strtok(buffer, ",");
  bool hasAny = false;

  while (token)
  {
    bool matched = false;
    for (int i = 0; knownKeys[i]; ++i)
    {
      size_t len = strlen(knownKeys[i]);
      if (strncmp(token, knownKeys[i], len) == 0)
      {
        const char *value = token + len;
        params[knownKeys[i]] = value;
        matched = true;
        hasAny = true;
        break;
      }
    }

    // –ü—Ä–æ–≤–µ—Ä–∫–∞ –Ω–∞ —Ç–∞–π–º–µ—Ä (—Ñ–æ—Ä–º–∞—Ç HH:MM)
    if (!matched && strchr(token, ':') && strlen(token) <= 5)
    {
      params["timer"] = token;
      hasAny = true;
    }

    token = strtok(nullptr, ",");
  }

  if (hasAny)
  {
    String jsonOut;
    serializeJson(doc, jsonOut);
    mqttClient.publish("esp/config", jsonOut.c_str());
  }
  else
  {
    StaticJsonDocument<128> rawDoc;
    rawDoc["type"] = "raw";
    rawDoc["line"] = rawLine;
    rawDoc["device_id"] = MQTT_CLIENT_ID;

    String jsonRaw;
    serializeJson(rawDoc, jsonRaw);
    mqttClient.publish("esp/raw", jsonRaw.c_str());
  }
}
